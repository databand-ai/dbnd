plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "com.vanniktech.maven.publish" version "0.22.0-SNAPSHOT" apply false
}

/**
 * ****************************************************************************
 * All projects configuration (java plugin, publishing, etc).
 * ****************************************************************************
 */
allprojects {
    apply plugin: 'java'

    group = 'ai.databand'
    version = '1.0.4.17'
    if (project.hasProperty('snapshot')) {
        version = "1.0.4.17-${System.env.CI_COMMIT_SHORT_SHA}"
    }
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    compileJava {
        options.compilerArgs << '-parameters'
    }

    test {
        useJUnitPlatform()
    }

    configurations.all {
        exclude group: "junit", module: "junit"
    }
}

def projectsToPublish = ["dbnd-api", "dbnd-api-deequ", "dbnd-client", "dbnd-mlflow", "dbnd-agent", "dbnd-azkaban-agent"]

configure(subprojects.findAll { projectsToPublish.contains(it.name) }) {
    apply plugin: "com.vanniktech.maven.publish"

    publishing {
        repositories {
            maven {
                url "https://gitlab.com/api/v4/projects/7965327/packages/maven"
                name "GitLab"
                credentials(HttpHeaderCredentials) {
                    name = 'Job-Token'
                    value = System.getenv("CI_JOB_TOKEN")
                }
                authentication {
                    header(HttpHeaderAuthentication)
                }
            }
        }
    }
}

/**
 * ****************************************************************************
 * Projects with fat jars.
 * ****************************************************************************
 */
def fatJarProjects = ["dbnd-agent", "dbnd-azkaban-event-reporter", "dbnd-azkaban-agent", "dbnd-examples"]

configure(subprojects.findAll { fatJarProjects.contains(it.name) }) {
    apply plugin: 'com.github.johnrengelman.shadow'

    task shadowJarLatest(type: Copy) {
        from shadowJar
        into 'build/libs'
        rename { fileName ->
            fileName.replace("$project.version", "latest")
        }
    }

    task fatJar(dependsOn: shadowJar)
    fatJar.finalizedBy shadowJarLatest
}

/**
 * ****************************************************************************
 * API-only projects (no real implementation goes here).
 * ****************************************************************************
 */
def apiOnlyProjects = ['dbnd-api'].toSet()

configure(subprojects.findAll { !apiOnlyProjects.contains(it.name) }) {
    dependencies {
        // logging
        implementation "org.slf4j:slf4j-api:1.7.36"
        runtimeOnly "org.slf4j:slf4j-log4j12:1.7.36"
        // tests
        testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
        testImplementation 'org.hamcrest:hamcrest:2.2'
        testImplementation 'org.mockito:mockito-all:1.10.19'
    }

    test {
        useJUnitPlatform()
    }
}

/**
 * ****************************************************************************
 * Projects with dependency on Spark.
 * Spark dependencies should be declared directly,
 * because gradle does not resolve transitive dependencies
 * ****************************************************************************
 */
def sparkProjects = ['dbnd-examples', 'dbnd-client', 'dbnd-agent'].toSet()

configure(subprojects.findAll { sparkProjects.contains(it.name) }) {
    dependencies {
        // spark & scala
        compileOnly('org.apache.spark:spark-core_2.11:2.4.8') {
            transitive = false
        }
        compileOnly('org.apache.spark:spark-sql_2.11:2.4.8') {
            transitive = false
        }
        compileOnly('org.apache.spark:spark-catalyst_2.11:2.4.8') {
            transitive = false
        }
        testImplementation('org.apache.spark:spark-sql_2.11:2.4.8') {
            exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        }
    }
}

/**
 * ****************************************************************************
 * Javassist projects.
 * ****************************************************************************
 */
def javassistProjects = ["dbnd-agent", "dbnd-azkaban-agent"].toSet()

configure(subprojects.findAll { javassistProjects.contains(it.name) }) {
    dependencies {
        implementation "org.javassist:javassist:3.29.0-GA"
    }
}

/**
 * ****************************************************************************
 * Cross-project dependencies.
 * ****************************************************************************
 */

project(':dbnd-client') {
    dependencies {
        implementation project(':dbnd-api')
    }
}

project(':dbnd-api-deequ') {
    dependencies {
        implementation project(':dbnd-client')
    }
}

project(':dbnd-agent') {
    dependencies {
        implementation project(':dbnd-api')
        implementation project(':dbnd-api-deequ')
        implementation project(':dbnd-client')
    }
}

project(':dbnd-mlflow') {
    dependencies {
        implementation project(':dbnd-client')
    }
}

project(':dbnd-azkaban') {
    dependencies {
        implementation project(':dbnd-client')
    }
}

project(':dbnd-azkaban-event-reporter') {
    dependencies {
        implementation project(':dbnd-client')
        implementation project(':dbnd-azkaban')
    }
}

project(':dbnd-azkaban-agent') {
    dependencies {
        implementation project(':dbnd-client')
        implementation project(':dbnd-azkaban')
    }
}

project(':dbnd-examples') {
    dependencies {
        implementation project(':dbnd-api')
        implementation project(':dbnd-api-deequ')
        implementation project(':dbnd-client')
        implementation project(':dbnd-mlflow')
    }
}
