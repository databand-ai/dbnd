/**
 * ****************************************************************************
 * Dependency on Azkaban SPI. Since Azkaban doesn't have it's jars published
 * anywhere, we use our own custom build and repository.
 * ****************************************************************************
 */
def azkabanVersion = "3.91.0-86-g2e2d3e61"

dependencies {
    compileOnly("com.linkedin:azkaban-spi:${azkabanVersion}")
    compileOnly("com.linkedin:azkaban-common:${azkabanVersion}")
    compileOnly("com.linkedin:azkaban-exec-server:${azkabanVersion}")
    compileOnly("com.linkedin:azkaban-web-server:${azkabanVersion}")
}

/**
 * ****************************************************************************
 * Deequ should be excluded from all configurations because it's provided.
 * ****************************************************************************
 */
configurations.all {
    exclude group: "com.amazon.deequ", module: "deequ"
}

/**
 * ****************************************************************************
 * We don't want all transitive dependencies to be included into fatjar.
 * Spark is provided dependency and we need to include only databand, retrofit,
 * slf4j and javassist.
 * ****************************************************************************
 */
shadowJar {
    dependencies {
        exclude(dependency {
            !it.moduleGroup.contains('databand') \
              && !it.moduleGroup.contains('squareup') \
              && !it.moduleGroup.contains('ch.qos') \
              && !it.moduleGroup.contains('javassist')
        })
    }
}
